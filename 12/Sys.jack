// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. 
    * calls the init functions of the other OS classes and then
    * calls the Main.main() function. For internal use only;*/
    function void init() {

        do Memory.init();
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        while(true){}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int i, j;
        let i = 0;
        let j = 0;

        // Duration must be positive
        if (~(duration > 0)){
            do Sys.error(1);
        }
        while(i < duration){
            while (j < 75){ 
                let j = j + 1; 
            }
            let j = 0;
            let i = i + 1;    
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printString("Err");
        do Output.printInt(errorCode);
        do Sys.halt();
        
        return;
    }
}
